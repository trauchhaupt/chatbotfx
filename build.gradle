plugins {
    id 'application' // For defining the main application
    id 'org.openjfx.javafxplugin' version '0.1.0' // JavaFX plugin
    id 'org.gradlex.extra-java-module-info' version '1.13.1'
    id 'idea'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'jakarta.validation:jakarta.validation-api:3.1.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0' // has no module info
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.0'
    implementation libs.lombok
    implementation libs.slf4.api
    //implementation libs.slf4.simple
    //implementation 'org.projectlombok:lombok:1.18.36'
    //implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'org.jsoup:jsoup:1.18.1'
    implementation 'commons-io:commons-io:2.20.0'
    implementation 'io.github.ollama4j:ollama4j:1.1.3'
}

javafx {
    version = '25.0.1' // Set the JavaFX version
    modules = ['javafx.controls', 'javafx.fxml'] // Add additional modules as needed
}

extraJavaModuleInfo{
    module('io.github.ollama4j:ollama4j', 'io.github.ollama4j') {
        exports("io.github.ollama4j")
        exports("io.github.ollama4j.utils")
        exports("io.github.ollama4j.exceptions")
        exports("io.github.ollama4j.models.response")
        exports("io.github.ollama4j.models.generate")
        exports("io.github.ollama4j.models.chat")
        exports("io.github.ollama4j.models.request")
        requires("org.slf4j")
        requires("simpleclient")
        requires("java.net.http")
        requires("com.fasterxml.jackson.databind")
        requires("com.fasterxml.jackson.datatype.jsr310")
    }
    module('simpleclient-0.16.0.jar', 'simpleclient', '0.16.0')
    module('simpleclient_tracer_otel-0.16.0.jar', 'simpleclient_tracer_otel', '0.16.0')
    module('simpleclient_tracer_otel_agent-0.16.0.jar', 'simpleclient_tracer_otel_agent', '0.16.0')
    module('listenablefuture-9999.0-empty-to-avoid-conflict-with-guava.jar', 'listenablefuture', '')
    module('simpleclient_tracer_common-0.16.0.jar', 'simpleclient_tracer_common', '0.16.0')
}

application {
    mainClass = 'de.vrauchhaupt.chatbotfx.ChatBot' // Replace with your main class
    mainModule = "chatbotfx"
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}